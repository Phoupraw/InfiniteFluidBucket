//file:noinspection GroovyAssignabilityCheck
plugins {
	id 'fabric-loom' version '1.7-SNAPSHOT'
	id 'maven-publish'
}

version = project.version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/"
		content {
			includeGroup("com.terraformersmc")
			includeGroup("dev.emi")
		}
	}
	maven {
		name = "Xander Maven"
		url = "https://maven.isxander.dev/releases"
		content {
			includeGroup("dev.isxander.yacl") //yet-another-config-lib-fabric
		}
	}
	maven {
		url = "https://maven.quiltmc.org/repository/release"
		content {
			includeGroup("org.quiltmc.parsers")
		}
	}
	maven {
		url = "https://oss.sonatype.org/content/repositories/snapshots"
		content {
			includeGroupAndSubgroups("com.twelvemonkeys")
		}
	}
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup("maven.modrinth")
		}
	}
}

fabricApi {
	configureDataGeneration()
}

dependencies {
	//## 基础依赖
	minecraft "com.mojang:minecraft:${minecraft}"
	mappings "net.fabricmc:yarn:${yarn_mappings}:v2"
	modApi "net.fabricmc:fabric-loader:${loader}"
	modApi "net.fabricmc.fabric-api:fabric-api:${fabric_api}"
	//## mod依赖
	/*modLocalRuntime*/ modApi("com.terraformersmc:modmenu:${modmenu}")
	//## 非mod依赖
	compileOnlyApi annotationProcessor("org.projectlombok:lombok:${lombok}")
	//## 非依赖
	modCompileOnly /*modLocalRuntime*/("maven.modrinth:vmp-fabric:${vmp}")
	modCompileOnlyApi("maven.modrinth:yacl:${yacl}")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}" }
	}
	exclude("net/fabricmc/*")
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {

	}
}